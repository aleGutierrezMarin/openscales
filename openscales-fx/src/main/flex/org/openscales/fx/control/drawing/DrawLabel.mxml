<?xml version="1.0" encoding="utf-8"?>
<os:IconButton xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:os="org.openscales.fx.control.*"
			   implements="org.openscales.core.control.IControl"
			   toolTip="{this.tooltip}"
			   icon="{_btnLabel}"
			   alpha="{_alpha}"
			   width="25"
			   height="25"
			   click="onClick()"
			   enabled="{active}">
	
	<fx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import org.openscales.core.Map;
			import org.openscales.core.control.IControl;
			import org.openscales.core.events.DrawingEvent;
			import org.openscales.core.events.FeatureEvent;
			import org.openscales.core.events.I18NEvent;
			import org.openscales.core.feature.Feature;
			import org.openscales.core.feature.LabelFeature;
			import org.openscales.core.feature.PointFeature;
			import org.openscales.core.handler.IHandler;
			import org.openscales.core.i18n.Catalog;
			import org.openscales.core.layer.VectorLayer;
			import org.openscales.core.style.Style;
			import org.openscales.fx.control.drawing.popup.EditLabel;
			import org.openscales.fx.control.layer.LayerManager;
			import org.openscales.geometry.LabelPoint;
			import org.openscales.geometry.Point;
			import org.openscales.geometry.basetypes.Location;
			import org.openscales.geometry.basetypes.Pixel;
			
			import spark.components.Button;
			import spark.components.HGroup;
			import spark.components.Label;
			import spark.components.TextInput;
			import spark.components.TitleWindow;
			
			[Bindable]
			private var _alpha:Number = 0.3;
			
			[Bindable]
			private var tooltip:String = Catalog.getLocalizationForKey('drawing.label');
			
			[Bindable]
			[Embed(source="/assets/images/icons/Icons-11.png")]
			private var _btnLabel:Class;
			
			private var _active:Boolean = true;
			private var _drawingActivated:Boolean = false;
			private var _isClick:Boolean = false;
			private var _style:Style = Style.getDefinedLabelStyle("Arial",12,0,false,false);
			private var _map:Map;
			private var _target:VectorLayer;
			

			private var feature:LabelFeature;
			
			/**
			 * Flag for activating and deactivating the component
			 * @default true;
			 */
			[Bindable]
			public function get active():Boolean{
				
				return _active;
			}
			public function set active(value:Boolean):void{
				
				_active = value;
			}
			
			/**
			 * Drawing style of the label
			 */
			public function get style():Style{
				
				return this._style;
			}
			public function set style(value:Style):void{
				
				this._style = value;
			}
			
			/**
			 * Map controled by this control
			 */
			public function get map():Map{
				
				return this._map;
			}
			public function set map(value:Map):void{
				
				this._map = value;
				this._map.addEventListener(DrawingEvent.DRAW_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.EDIT_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.MOVE_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.DELETE_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.SELECT_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.CHANGE_ACTIVE_LAYER, onChangeActiveLayer);
				this._map.addEventListener(I18NEvent.LOCALE_CHANGED, onMapLanguageChange);
				this._map.addEventListener(MouseEvent.CLICK, this.createLabel);
			}
			
			/**
			 * To create a LabelFeature
			 */
			private function createLabel(event:MouseEvent):void{
				
				if(this._drawingActivated)
				{
					var pixel:Pixel = new Pixel(this._map.mouseX, this._map.mouseY);
					var labelLocation:Location = this._map.getLocationFromMapPx(pixel);
					feature = new LabelFeature(new LabelPoint(null, labelLocation.x, labelLocation.y));
					feature.layer = this.target;
					
					var editLabel:EditLabel = new EditLabel();
					editLabel.containerPopup = this;
					editLabel.feature= feature; 
				}
			}
			
			/**
			 * Position (x,y) of the handler
			 */
			public function get position():Pixel{
				
				return new Pixel(x,y);
			}
			public function set position(value:Pixel):void{
				
				if(value){
					x = value.x;
					y = value.y;
				}
			}
			
			/**
			 * Layer the features will be drawn on
			 */
			public function get target():VectorLayer{
				
				return this._target;
			}
			public function set target(value:VectorLayer):void{
				this.disactivate();
				this._target = value;
			}
			
			/**
			 * To activate or disactivate the handler
			 */
			public function activate():void{
				
				this._drawingActivated = true;
				_alpha = 1;
				this._map.mouseNavigationEnabled = false;
				this._map.panNavigationEnabled = false;
				this._map.zoomNavigationEnabled = false;
			}
			public function disactivate():void{
				
				this._drawingActivated = false;
				_alpha = 0.3;
			}
			
			/**
			 * On button click, dispatch a DrawingEvent.DRAW_HANDLER_ACTIVATED event
			 */
			public function onClick():void{
				
				if(!this.target)
					return;
				
				// Disactive this component
				var evt:DrawingEvent;
				if(this._drawingActivated){
					disactivate();
					this._map.mouseNavigationEnabled = true;
					this._map.panNavigationEnabled = true;
					this._map.zoomNavigationEnabled = true;
					return;
				}
				
				// Disactive others drawing tools components
				this._isClick = true;
				evt = new DrawingEvent(DrawingEvent.DRAW_HANDLER_ACTIVATED);
				evt.activeHandler = this.id;
				evt.layer = this.target;
				this._map.dispatchEvent(evt);
				
				// Disactive mesure tools components
				evt = new DrawingEvent(DrawingEvent.CHANGE_ACTIVE_HANDLER);
				evt.activeHandler = null;
				this._map.dispatchEvent(evt);
				
				// Active this component
				activate();
			}
			
			/**
			 * Check the handler when a DrawingEvent.DRAW_HANDLER_ACTIVATED or
			 * DrawingEvent.EDIT_HANDLER_ACTIVATED or DrawingEvent.MOVE_HANDLER_ACTIVATED
			 * or DrawingEvent.SELECT_HANDLER_ACTIVATED event occured
			 */
			private function onChangeActiveHandler(evt:DrawingEvent):void{
				
				if(this._drawingActivated && !this._isClick)
					disactivate();
				else
					this._isClick = false;
			}
			
			/**
			 * Update the target on active layer changed
			 */
			private function onChangeActiveLayer(evt:DrawingEvent):void{
				
				if(this.parentDocument is DrawingToolbar)
				{
					if(this._drawingActivated){
						disactivate();
						this._map.mouseNavigationEnabled = true;
						this._map.panNavigationEnabled = true;
						this._map.zoomNavigationEnabled = true;
					}
					this.target = evt.layer;
				}
			}
			
			/**
			 * Update texts on map language changed
			 */
			public function onMapLanguageChange(event:I18NEvent):void{
				
				this.tooltip = Catalog.getLocalizationForKey('drawing.label');
			}
			
			// Unused functions
			public function draw():void{
				
				this.invalidateDisplayList();
			}
			public function destroy():void{}
		]]>
	</fx:Script>			
</os:IconButton>
