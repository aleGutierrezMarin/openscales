<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:openscales="http://openscales.org" >
	
	<s:states>
		<s:State name="normal" />
		<s:State name="popup" />
	</s:states>
	
	<s:Button label="{this.tooltipImport}" toolTip="{this.tooltipImport}" click="{this.importFeature(event)}" />
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import org.openscales.core.Map;
			import org.openscales.core.events.I18NEvent;
			import org.openscales.core.i18n.Catalog;
			import org.openscales.core.layer.VectorLayer;
			import org.openscales.core.layer.ogc.GML;
			import org.openscales.core.layer.osm.OSM;
			
			import spark.components.TitleWindow;
			
			/**
			 * Tooltip that appears over Import button
			 */
			[Bindable]
			public var tooltipImport:String = Catalog.getLocalizationForKey('drawing.import');
	
			private var _layer:VectorLayer = null;

			/**
			 * The title window (that display the form)
			 */
			public var myTitleWindow:TitleWindow = new TitleWindow();
			
			/**
			 * Change the text values on Map Language changed
			 * @param event The I18N event received
			 */
			public function onMapLanguageChange(event:I18NEvent):void 
			{
				this.tooltipImport = Catalog.getLocalizationForKey('drawing.import');
			}
	
			/**
			 * Close the PopUp window
			 */
			public function closePopUp(event:MouseEvent = null):void
			{
				// close the pop up file
				PopUpManager.removePopUp(myTitleWindow);
			}

			/**
			 * Called when the user click on the import button
			 * Make the pop up visible
			 * 
			 * @param event The MouseEvent received
			 */
			public function importFeature(event:MouseEvent):void {
				// Set the TitleWindow container properties.
				myTitleWindow = new TitleWindow();
				myTitleWindow.title = Catalog.getLocalizationForKey('drawing.import');
				myTitleWindow.addEventListener(FlexEvent.CREATION_COMPLETE, this.onPopUpCreationComplete);
				
				// Call the method to add the Button control to the 
				// TitleWindow container.
				populateWindow();
				
				// place at the center
				myTitleWindow.x = (this.layer.map.width/2)-(this.width/2);
				myTitleWindow.y = (this.layer.map.height/2)-(this.height/2);
				
				// Use the PopUpManager to display the TitleWindow container.
				PopUpManager.addPopUp(myTitleWindow, this, true);
			}
			
			private function onPopUpCreationComplete(e:Event):void
			{
				myTitleWindow.closeButton.addEventListener(MouseEvent.CLICK, this.closePopUp);
			}
			
			/**
			 * Function that fill the popUp
			 */
			private function populateWindow():void 
			{	
				var form:ImportFeatureForm = new ImportFeatureForm();
				form.importFeature = this;
				myTitleWindow.addElement(form); 
			}
			
			/**
			 * The layer linked to the import button
			 */
			public function get layer():VectorLayer
			{
				return this._layer;
			}
			
			/**
			 * @private
			 */
			public function set layer(value:VectorLayer):void
			{
				this._layer = value;
				if(this._layer.map)
				{
					this._layer.map.addEventListener(I18NEvent.LOCALE_CHANGED, this.onMapLanguageChange);
				}
			}


		]]>
	</fx:Script>
	
</s:Group>