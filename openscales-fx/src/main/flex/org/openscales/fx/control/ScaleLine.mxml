<?xml version="1.0" encoding="utf-8"?>
<Control xmlns="http://openscales.org" 
		 xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 width="400" height="300" >
	
	<fx:Script>
		<![CDATA[
			import flash.events.Event;
			import flash.text.TextField;
			import flash.text.TextFormat;
			
			import org.openscales.core.Map;
			import org.openscales.core.Trace;
			import org.openscales.core.events.LayerEvent;
			import org.openscales.core.events.MapEvent;
			import org.openscales.core.i18n.Catalog;
			import org.openscales.geometry.basetypes.Pixel;
			import org.openscales.geometry.basetypes.Unit;
			import org.openscales.proj4as.ProjProjection;
			
			/**
			 * @private
			 * Maximum width of the scale line in pixels.  
			 * @default 100.
			 */
			private var _scaleMaxWidth:Number = 100;
			
			/**
			 * @private
			 * Scale value corresponding to the current rectangle size for top Unit
			 * @default 0
			 */
			[Bindable]
			private var _topScaleValue:Number = 0;
			
			/**
			 * @private
			 * The current unit for the top scale value
			 * @default null
			 */
			private var _topCurrentUnit:String = null;
			
			/**
			 * @private
			 * Units for zoomed out on top bar.
			 * @default km.
			 */
			private var _topOutUnits:String = Catalog.getLocalizationForKey("scaleline.km");
			
			/**
			 * @private
			 * Units for zoomed in on top bar.  
			 * @default m.
			 */
			private var _topInUnits:String = Catalog.getLocalizationForKey("scaleline.m");
			
			/**
			 * @private
			 * Scale value corresponding to the current rectangle size for bottom Unit
			 * @default 0
			 */
			[Bindable]
			private var _bottomScaleValue:Number = 0;
			
			/**
			 * @private
			 * The current unit for the bottom scale value
			 * @default null
			 */
			private var _bottomCurrentUnit:String = null;
			
			/**
			 * @private
			 * Units for zoomed out on bottom bar.
			 * @default mi.
			 */
			private var _bottomOutUnits:String = Catalog.getLocalizationForKey("scaleline.mi");
			
			/**
			 * @private
			 * Units for zoomed in on bottom bar.
			 * @default ft
			 */
			private var _bottomInUnits:String = Catalog.getLocalizationForKey("scaleline.ft");
			
			
			/**
			 * Top scale width
			 * @default 0
			 */
			[Bindable]
			public var topScaleWidth:Number = 0;
			
			/**
			 * Bottom scale width
			 * @default 0
			 */
			[Bindable]
			public var bottomScaleWidth:Number = 0;
			
			/**
			 * Get the existing map and add event listener on event Zoom end and baseLayer change.
			 *
			 * @param value The new map to set
			 */
			override public function set map(value:Map):void 
			{
				super.map = value;
				
				if(this._map)
					this.updateScale();
			}
			
			/**
			 * @inherit
			 */
			override public function activate():void 
			{
				super.activate();
				
				// add listener to the map
				if(this._map)
				{
					this._map.addEventListener(MapEvent.MOVE_END,updateScaleLineOnMove);
					this._map.addEventListener(LayerEvent.BASE_LAYER_CHANGED,updateScaleLineOnBaseLayerChanged);
				}
			}
			
			/**
			 * @inherit
			 */
			override public function desactivate():void 
			{
				super.desactivate();
				
				// remove listener to the map
				if(this._map)
				{
					this._map.removeEventListener(MapEvent.MOVE_END,updateScaleLineOnMove);
					this._map.removeEventListener(LayerEvent.BASE_LAYER_CHANGED,updateScaleLineOnBaseLayerChanged);
				}
			}
			
			/**
			 * Redraw the scaleline with new parameters.
			 * 
			 * @param event the event can be a MapEvent.MOVE_END.
			 */
			public function updateScaleLineOnMove(e:MapEvent):void {
				if(e.zoomChanged)
					this.updateScale();
			}
			
			public function updateScaleLineOnBaseLayerChanged(e:LayerEvent):void {
				this.updateScale();
			}
			
			
			
			/**
			 * Given a number, round it down to the nearest 1,2,5 times a power of 10.
			 * That seems a fairly useful set of number groups to use.
			 *
			 * @param maxLen the number we're rounding down from
			 *
			 * @return the rounded number (less than or equal to maxLen)
			 */
			private function getBarLen(maxLen:Number):Number 
			{
				// nearest power of 10 lower than maxLen
				var digits:Number = int(Math.log(maxLen) / Math.log(10));
				var pow10:Number = Math.pow(10, digits);
				
				// ok, find first character
				var firstChar:Number = int(maxLen / pow10);
				
				// right, put it into the correct bracket
				var barLen:Number;
				if(firstChar > 5) {
					barLen = 5;
				} else if(firstChar > 2) {
					barLen = 2;
				} else {
					barLen = 1;
				}
				
				// scale it up the correct power of 10
				return barLen * pow10;
			}
			
			/**
			 * Update the size of the bars, and the labels which contains.
			 */
			private function updateScale():void
			{
				// Get the resolution of the map
				var mapResolution:Number = this.map.resolution;
				
				// Map has no resolution, return.
				if (!mapResolution) {return;}
				
				// get the current units of the map
				var currentBaseLayerUnits:String = ProjProjection.getProjProjection(this.map.baseLayer.projSrsCode).projParams.units;
				
				// convert the scaleMaxWidth to map units
				// The result is the max distance IN MAP UNIT, represent in the scaleline
				var maxSizeData:Number = this._scaleMaxWidth * mapResolution * Unit.getInchesPerUnit(currentBaseLayerUnits);
				
				// decide whether to use large or small scale units. it's independent of the map unit    
				if(maxSizeData > 100000) {
					this.topCurrentUnit = this.topOutUnits;
					this.bottomCurrentUnit = this._bottomOutUnits;
				} else {
					this.topCurrentUnit = this.topInUnits;
					this.bottomCurrentUnit = this._bottomInUnits;
				}
				
				// and to map units units
				var topMax:Number = maxSizeData / Unit.getInchesPerUnit(this._topCurrentUnit);
				var bottomMax:Number = maxSizeData / Unit.getInchesPerUnit(this._bottomCurrentUnit);
				
				// now trim this down to useful block length
				this.topScaleValue = this.getBarLen(topMax);
				this.bottomScaleValue = this.getBarLen(bottomMax);
				
				// and back to display units
				topMax = this._topScaleValue / Unit.getInchesPerUnit(currentBaseLayerUnits) * Unit.getInchesPerUnit(this._topCurrentUnit);
				bottomMax = this._bottomScaleValue / Unit.getInchesPerUnit(currentBaseLayerUnits) * Unit.getInchesPerUnit(this._bottomCurrentUnit);
				
				// and to pixel units
				var topPx:Number = topMax / mapResolution;
				var bottomPx:Number = bottomMax / mapResolution;
				
				// update the top rect width
				this.topScaleWidth = Math.round(topPx);
				this.bottomScaleWidth = Math.round(bottomPx);
				
			}	
			
			// GETTERS AND SETTERS
			
			/**
			 * Maximum width of the scale line in pixels. 
			 */
			public function get scaleMaxWidth():Number {
				return this._scaleMaxWidth;
			}
			
			/**
			 * @private
			 */
			public function set scaleMaxWidth(value:Number):void {
				this._scaleMaxWidth = value;
			}
			
			/**
			 * Scale value corresponding to the current rectangle size for top Unit
			 */
			[Bindable]
			public function get topScaleValue():Number
			{
				return this._topScaleValue;
			}
			
			/**
			 * @private
			 */
			public function set topScaleValue(value:Number):void
			{
				this._topScaleValue=value;
			}
			
			/**
			 * Scale value corresponding to the current rectangle size for top Unit
			 */
			[Bindable]
			public function get topCurrentUnit():String
			{
				return this._topCurrentUnit;
			}
			
			/**
			 * @private
			 */
			public function set topCurrentUnit(value:String):void
			{
				this._topCurrentUnit=value;
			}
			
			/**
			 * Units for zoomed out on top bar.
			 */
			public function get topOutUnits():String {
				return this._topOutUnits;
			}
			
			/**
			 * @private
			 */
			public function set topOutUnits(value:String):void {
				this._topOutUnits = value;
			}
			
			/**
			 * Units for zoomed in on top bar. 
			 */
			public function get topInUnits():String {
				return this._topInUnits;
			}
			
			/**
			 * @private
			 */
			public function set topInUnits(value:String):void {
				this._topInUnits = value;
			}
			
			/**
			 * Scale value corresponding to the current rectangle size for bottom Unit
			 */
			[Bindable]
			public function get bottomScaleValue():Number
			{
				return this._bottomScaleValue;
			}
			
			/**
			 * @private
			 */
			public function set bottomScaleValue(value:Number):void
			{
				this._bottomScaleValue=value;
			}
			
			/**
			 * Scale value corresponding to the current rectangle size for bottom Unit
			 */
			[Bindable]
			public function get bottomCurrentUnit():String
			{
				return this._bottomCurrentUnit;
			}
			
			/**
			 * @private
			 */
			public function set bottomCurrentUnit(value:String):void
			{
				this._bottomCurrentUnit=value;
			}
			
			/**
			 * Units for zoomed out on bottom bar.
			 */
			[Bindable]
			public function get bottomOutUnits():String {
				return this._bottomOutUnits;
			}
			
			/**
			 * @private
			 */
			public function set bottomOutUnits(value:String):void {
				this._bottomOutUnits = value;
			}
			
			/**
			 * Units for zoomed in on bottom bar.
			 */
			public function get bottomInUnits():String {
				return this._bottomInUnits;
			}
			
			/**
			 * @private
			 */
			public function set bottomInUnits(value:String):void {
				this._bottomInUnits = value;
			}
			
			
		]]>
	</fx:Script>
	
	
	<s:Label id="scaleTopValue" x="13" y="0" text="{this.topScaleValue + ' ' + this.topCurrentUnit}" />
	<s:Rect id="scaleTopWidth" x="13" y="12" height="8" width="{this.topScaleWidth}">     
		<s:fill>
			<s:SolidColor color="0x666666" alpha=".5"/>
		</s:fill>
	</s:Rect>
	
	<s:Label id="scaleBottomValue" x="13" y="33" text="{this.bottomScaleValue + ' ' + this.bottomCurrentUnit}" />
	<s:Rect id="scaleBottomWidth" x="13" y="23" height="8" width="{this.bottomScaleWidth}">
		<s:fill>
			<s:SolidColor color="0x666666" alpha=".5"/>
		</s:fill>
	</s:Rect>
	
</Control>
