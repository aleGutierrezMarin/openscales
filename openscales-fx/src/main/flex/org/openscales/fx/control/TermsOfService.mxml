<?xml version="1.0" encoding="utf-8"?>
<!--- Terms Of Service control to display a label and a link to Terms of service explanations page  -->
<Control xmlns="http://openscales.org" 
		 xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 width="400" height="100" initialize="configure()">
	
	<fx:Script>
		<![CDATA[
			import org.openscales.core.events.I18NEvent;
			import org.openscales.core.i18n.Catalog;
			
			import spark.utils.TextFlowUtil;
			
			
			private var _autoTranslate:Boolean = true;
			[Bindable] private var _label:String = Catalog.getLocalizationForKey("tos.label");
			[Bindable] private var _url:String = "";
			
			public function  configure():void
			{ 
				this.updateTextLabel();	
			}
			
			/**
			 * Label displayed for the Terms of service link.
			 * @return The string value of the label (default : "Terms of Service")
			 */
			public function get label():String{
				return this._label;
			}
			
			public function set label(value:String):void{
				this._autoTranslate = false;
				this._label = value;
				this.updateTextLabel();
			}
			
			/**
			 * The url to the page where terms of Service are explained.
			 * @return The string value of the url
			 */
			public function get url():String{
				return this._url;
			}
			
			public function set url(value:String):void{
				this._url = value;
			}
			
			/**
			 * Update the current text label (get the current language)
			 */
			public function updateTextLabel():void
			{
				if(this._autoTranslate) {
					this._label = Catalog.getLocalizationForKey("tos.label");
				}
				
				termsOfServiceText.text = this._label;
			}
			
			/**
			 * @inherit
			 */
			override public function onMapLanguageChange(event:I18NEvent):void {
				this.updateTextLabel();
			}
			
			/**
			 * Function called on click
			 * 
			 * @param event The click mouse event received
			 */
			public function onClick(event:Event):void
			{
				// open a new page with the originator url
				var targetURL:URLRequest = new URLRequest(this._url);
				navigateToURL(targetURL);
			}
		]]>
	</fx:Script>
	
	<s:Label id="termsOfServiceText"
			 buttonMode="true"
			 useHandCursor="true"
			 click="onClick(event)"
			 width="100%"
			 height="100%" />
	
</Control>