<?xml version="1.0" encoding="utf-8"?>
<Control xmlns="org.openscales.component.control.*"
		 xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 width="200"
		 height="300"
		 creationComplete="finalizeTraceInfo();">
	
	<fx:Script>
		<![CDATA[
			import flashx.textLayout.conversion.TextConverter;
			
			import org.openscales.core.Map;
			import org.openscales.core.Trace;
			import org.openscales.core.events.TraceEvent;
			
			import spark.skins.spark.ButtonSkin;
			import spark.utils.TextFlowUtil;
			
			private var text:String = '<b>Output Console :</b>';
			[Bindable] public var _clearButton:Boolean = true;
			public function get clearButton():Boolean {
				return _clearButton;
			}
			
			public function set clearButton(value:Boolean):void {
				_clearButton = value;
				if (clearLogsButton) {
					clearLogsButton.visible = _clearButton;
				}
			}
			
			override public function set map(value:Map):void {
				if(value) {
					super.map = value;
					this.map.addEventListener(TraceEvent.LOG, displayLog);
					this.map.addEventListener(TraceEvent.INFO, displayLog);
					this.map.addEventListener(TraceEvent.WARNING, displayLog);
					this.map.addEventListener(TraceEvent.ERROR, displayLog);
					this.map.addEventListener(TraceEvent.DEBUG, displayLog);
				}
			}
			
			private function finalizeTraceInfo():void {
				clearLogsButton.visible = clearButton;
				clearTrace();
			}
			
			private function clearTrace():void {
				text = '<b>Output Console :</b>';
				textArea.textFlow = TextConverter.importToFlow(text, TextConverter.TEXT_FIELD_HTML_FORMAT);
			}
			
			private function displayLog(event:TraceEvent):void {
				var textColor:String = "#000066"; // used if event.type is TraceEvent.LOG or TraceEvent.INFO
				if (event.type == TraceEvent.WARNING) {
					textColor = "#FFCC00";
				} else if (event.type == TraceEvent.ERROR) {
					textColor = "#FF0000";
				} else if (event.type == TraceEvent.DEBUG) {
					textColor = "#0066FF";
				}
				text += "<br/><font color='"
					+textColor
					+"' size='9'>"
					+event.text
					+"</font><br/>----------";
				
				textArea.textFlow = TextConverter.importToFlow(text, TextConverter.TEXT_FIELD_HTML_FORMAT);
			}
		]]>
	</fx:Script>
	
	<s:VGroup width="100%"
			  height="100%"
			  horizontalAlign="center">
		<s:TextArea id="textArea"
					verticalScrollPolicy="on"
					horizontalCenter="0"
					verticalCenter="0"
					width="100%"
					height="100%"/>
		<s:Button id="clearLogsButton"
				  label="Clear logs"
				  click="clearTrace()"/>
	</s:VGroup>
</Control>

