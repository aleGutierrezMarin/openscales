<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009"
		  xmlns:s="library://ns.adobe.com/flex/spark"
		  width="100%">
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.collections.ArrayList;
			import mx.events.SliderEvent;
			
			import org.openscales.core.Map;
			import org.openscales.core.events.LayerEvent;
			import org.openscales.core.layer.Layer;
			
			import spark.events.TrackBaseEvent;
			
			[Bindable]
			private var _baselayerArray:Vector.<Layer>;
			
			[Bindable]
			private var _baseLayerPrompt:String = "----";
			
			[Bindable]
			private var _baseLayerVisibility:Number=100;
			
			/**
			 * Change the opacity of the layer
			 *
			 * @param event event send by a slide
			 */
			public function layerOpacity(event:Event):void
			{
				layer.alpha = (event.target as HSlider).value/100;
			}
			
			private function onDrag(event:Event):void {
				event.stopPropagation();
			}
			
			/**
			 * Add name of layer into the comboBox
			 * 
			 * @param item it's a layer
			 */
			private function baselayerCombobox_labelFunc(item:String):String {
				return item;
			}
			/**
			 * Change the base layer
			 * 
			 * @param event a comboBox event
			 */
			private function closeHandler(event:Event):void {
				if((event.target as ComboBox).selectedItem != null)
				{
					if(this.layer.map.baseLayer.name != (event.target as ComboBox).selectedItem as String)
					{
						layer.map.baseLayer.visible = false;
						layer.map.baseLayer = layer.map.getLayerByName((event.currentTarget as ComboBox).selectedItem as String);
						layer.map.baseLayer.visible = true;
						layer.map.baseLayer.redraw();
						opacityControl.value = layer.map.baseLayer.alpha*100;
					}
				}    
			}
			//Getters and setters
			public function set baseLayerPrompt(value:String):void {
				this._baseLayerPrompt = value;
				this.BaselayerCombo.selectedItem = value;
			}
			
			public function set baseLayerVisibility(value:Number):void {
				this._baseLayerVisibility = value*100;
			}
			
			public function get baseLayerArray():Vector.<Layer> {
				return this._baselayerArray;
			}
			
			public function set baseLayerArray(value:Vector.<Layer>):void {
				this._baselayerArray = value;
				var i:uint = value.length;
				var ac:ArrayList = new ArrayList();
				for(var j:uint=0;j<i;++j)
					ac.addItem(value[j].name);
				BaselayerCombo.dataProvider = ac;
				
				
			}
			/**
			 * Get the layer selected into the combobox
			 *
			 * @return return the layer
			 */
			private function get layer():Layer {
				return ((this.parent.parent as LayerSwitcher).map.getLayerByName((this.BaselayerCombo.selectedItem as String)) as Layer)
			}
			
		]]>
	</fx:Script>
	
	<s:ComboBox id="BaselayerCombo"
				preinitialize="{_baseLayerPrompt}"
				labelFunction="baselayerCombobox_labelFunc"
				close="closeHandler(event);"/>
	<s:HGroup id="Hbox2"
			  width="100%">
		<s:Label text="0%"/>
		<s:HSlider id="opacityControl"
				   width="100"
				   height="5"
				   value="{this._baseLayerVisibility}"
				   minimum="0"
				   maximum="100"
				   change="layerOpacity(event)"
				   liveDragging="true"
				   mouseDown="onDrag(event)"/>
		<s:Label text="100%"/>
	</s:HGroup> 
</s:VGroup>
